  TITLE: Strong Invariance 1 freed wjap at time 1
  DATA: FILE = "moderation.4.dat";
  DEFINE: A1PPVT4W = A1PPVT4W/10;
          A1WJLWW = A1WJLWW/10;
          A2PPVT4W = A2PPVT4W/10;
          A2WJLWW = A2WJLWW/10;
          A3PPVT4W = A3PPVT4W/10;
          A3WJLWW = A3WJLWW/10;
          A1WJAPW = A1WJAPW/10;
          A2WJAPW = A2WJAPW/10;
          A3WJAPW = A3WJAPW/10;
  VARIABLE:
  NAMES = A1PPVT4W A1WJAPW A1WJLWW A1CSR A1ECMIRT A1WJSW R1ATTUDE R1MOTIVE R1PRSIST
       A2PPVT4W A2WJAPW A2WJLWW A2CSR A2ECMIRT A2WJSW R2ATTUDE R2MOTIVE R2PRSIST
       A3PPVT4W A3WJAPW A3WJLWW A3CSR A3ECMIRT A3WJSW KR3ATUDE KR3MOTIV KR3PRSST
       R1TPLBS R2TPLBS KR3TPLBS STRAT PSU PRA12OTW CHGENDER P1RMOMED A1CAGE A2CAGE
       A3CAGE CRACE P1PRGTYP P2PRGTYP P3PRGTYP O2CLSSCD O2CLSSIS O2CLSSLM O2CLSSQF
       CLS1_ID CLS2_ID CLS3_ID R1BAGGR R1BWITH R1BHYPER R1ATTUDE_1 R2BAGGR R2BWITH
       R2BHYPER KR3BAGGR KR3BHYPE KR3BWITH BINAGGR1 BINWITH1 BINHYP1 BINAGGR2 BINWITH2
       BINHYP2 BINAGGR3 BINWITH3 BINHYP3;
  USEVARIABLES = A1WJAPW A2WJAPW A3WJAPW A1PPVT4W A2PPVT4W A3PPVT4W A1CSR A2CSR A3CSR
       R1ATTUDE R1MOTIVE R1PRSIST R2ATTUDE R2MOTIVE R2PRSIST KR3MOTIV KR3PRSST
       KR3ATUDE STRAT PSU PRA12OTW;
  STRATIFICATION = STRAT;
  CLUSTER = PSU;
  WEIGHT = PRA12OTW;
  MISSING=.;
  ANALYSIS:
    TYPE = COMPLEX;
    ITERATIONS = 10000;
    ESTIMATOR =MLR;
  MODEL:
    cog1 BY A1WJAPW*
    A1PPVT4W(2)
    A1CSR(3);
    cog2 BY A2WJAPW*(1)
    A2PPVT4W(2)
    A2CSR(3);
    cog3 BY A3WJAPW*(1)
    A3PPVT4W(2)
    A3CSR(3);
    cog1@1.0;

    atl1 BY R1ATTUDE*(4)
    R1MOTIVE(5)
    R1PRSIST(6);
    atl2 BY R2ATTUDE*(4)
    R2MOTIVE(5)
    R2PRSIST(6);
    atl3 BY KR3ATUDE*(4)
    KR3MOTIV(5)
    KR3PRSST(6);
    atl1@1.0;

    !correlateed residuals for cognitive skills
    A1WJAPW WITH A2WJAPW A3WJAPW;
    A2WJAPW WITH A3WJAPW;
    A1PPVT4W WITH A2PPVT4W A3PPVT4W;
    A2PPVT4W WITH A3PPVT4W;
    A1CSR WITH A2CSR A3CSR;
    A2CSR WITH A3CSR;

    !correlated residuals for AtL
    R1ATTUDE WITH R2ATTUDE KR3ATUDE;
    R2ATTUDE WITH KR3ATUDE;
    R1MOTIVE WITH R2MOTIVE KR3MOTIV;
    R2MOTIVE WITH KR3MOTIV;
    R1PRSIST WITH R2PRSIST KR3PRSST;
    R2PRSIST WITH KR3PRSST;

    !intercept invariance
    [cog2 cog3];
    [atl2 atl3];
    [A1WJAPW A2WJAPW A3WJAPW](7);
    [A1PPVT4W A2PPVT4W A3PPVT4W](8);
    [A1CSR A2CSR A3CSR](9);

    [R1ATTUDE R2ATTUDE KR3ATUDE](10);
    [R1MOTIVE R2MOTIVE KR3MOTIV](11);
    [R1PRSIST R2PRSIST KR3PRSST](12);

  OUTPUT:
    TECH1
    SAMPSTAT
    modindices;
    STANDARDIZED;