 TITLE: Configural Invariance
  DATA: FILE = "moderation.4.dat";
  DEFINE:
  A1WJAPW = A1WJAPW/100;
  A2WJAPW = A2WJAPW/100;
  A3WJAPW = A3WJAPW/100;
  A1PPVT4W = A1PPVT4W/10;
  A2PPVT4W = A2PPVT4W/10;
  A3PPVT4W = A3PPVT4W/10;
  A1CSR = A1CSR/10;
  A2CSR = A2CSR/10;
  A3CSR = A3CSR/10;
  VARIABLE:
  NAMES = A1PPVT4W A1WJAPW A1WJLWW A1CSR A1ECMIRT A1WJSW R1ATTUDE R1MOTIVE R1PRSIST
       A2PPVT4W A2WJAPW A2WJLWW A2CSR A2ECMIRT A2WJSW R2ATTUDE R2MOTIVE R2PRSIST
       A3PPVT4W A3WJAPW A3WJLWW A3CSR A3ECMIRT A3WJSW KR3ATUDE KR3MOTIV KR3PRSST
       R1TPLBS R2TPLBS KR3TPLBS STRAT PSU PRA12OTW CHGENDER P1RMOMED P1RCAGE CRACE
       P1PRGTYP P2PRGTYP P3PRGTYP GEN1 GEN0 P1MOMED1 P1MOMED2 CRACE3 CRACE4 CRACE1
       CRACE2 CRACE3_1 CRACE4_1 CRACE5 CRACE6 CRACE7 PRG11 PRG12 PRG13 PRG21 PRG22
       PRG23 PRG34 PRG35;
  USEVARIABLES = A1WJAPW A2WJAPW A3WJAPW A1PPVT4W A2PPVT4W A3PPVT4W A1CSR A2CSR A3CSR
       R1ATTUDE R1MOTIVE R1PRSIST R2ATTUDE R2MOTIVE R2PRSIST KR3MOTIV KR3PRSST
       KR3ATUDE STRAT PSU PRA12OTW;
  STRATIFICATION = STRAT;
  CLUSTER = PSU;
  WEIGHT = PRA12OTW;
  MISSING=.;
  ANALYSIS:
    TYPE = COMPLEX;
    ITERATIONS = 10000;
    ESTIMATOR =MLR;
  MODEL:
    cog1 BY A1WJAPW* A1PPVT4W A1CSR;
    cog2 BY A2WJAPW* A2PPVT4W A2CSR;
    cog3 BY A3WJAPW* A3PPVT4W A3CSR;
    cog1@1.0;
    cog2@1.0;
    cog3@1.0;

    atl1 BY R1ATTUDE* R1MOTIVE R1PRSIST;
    atl2 BY R2ATTUDE* R2MOTIVE R2PRSIST;
    atl3 BY KR3ATUDE* KR3MOTIV KR3PRSST;
    atl1@1.0;
    atl2@1.0;
    atl3@1.0;

    !correlateed residuals for cognitive skills
    A1WJAPW WITH A2WJAPW A3WJAPW;
    A2WJAPW WITH A3WJAPW;
    A1PPVT4W WITH A2PPVT4W A3PPVT4W;
    A2PPVT4W WITH A3PPVT4W;
    A1CSR WITH A2CSR A3CSR;
    A2CSR WITH A3CSR;

    !correlated residuals for AtL
    R1ATTUDE WITH R2ATTUDE KR3ATUDE;
    R2ATTUDE WITH KR3ATUDE;
    R1MOTIVE WITH R2MOTIVE KR3MOTIV;
    R2MOTIVE WITH KR3MOTIV;
    R1PRSIST WITH R2PRSIST KR3PRSST;
    R2PRSIST WITH KR3PRSST;

  OUTPUT:
    TECH1
    SAMPSTAT
    modindices;
    STANDARDIZED;